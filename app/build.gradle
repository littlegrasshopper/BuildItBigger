apply plugin: 'com.android.application'
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:1.0.2'
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "tier"
    productFlavors {
        free {
            dimension "tier"
            applicationId "com.udacity.gradle.builditbigger.free"
        }
        paid {
            dimension "tier"
            applicationId "com.udacity.gradle.builditbigger.paid"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    endpointsServer project(path: ':backend', configuration: 'endpoints')
    implementation "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    implementation 'com.google.android.gms:play-services-ads:15.0.1'
    implementation "com.google.api-client:google-api-client:${rootProject.ext.googleClientVersion}"
    implementation "com.google.http-client:google-http-client-android:${rootProject.ext.googleClientVersion}"
    implementation 'com.google.code.findbugs:jsr305:3.0.1'
    implementation project(':javaJokes')
    implementation project(':jokeviewer')

    // Unit tests
    testImplementation "junit:junit:${rootProject.ext.junitLibVersion}"

    // Required for instrumented tests
    androidTestImplementation "com.android.support.test:runner:${rootProject.ext.testRunnerVersion}"
    androidTestImplementation "com.android.support.test:rules:${rootProject.ext.testRunnerVersion}"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:${rootProject.ext.expressoCoreVersion}", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Butterknife annotation
    implementation 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'

}

// Credit:
// https://docs.gradle.org/current/dsl/org.gradle.api.artifacts.ResolutionStrategy.html
configurations.all {
    resolutionStrategy {
        force "com.android.support:support-annotations:${rootProject.ext.supportLibVersion}"
    }
}
